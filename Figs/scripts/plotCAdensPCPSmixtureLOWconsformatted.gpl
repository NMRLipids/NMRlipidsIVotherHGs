set term pdfcairo enhanced color dashed dashlength 0.5 size 10cm, 10cm font "Arial,15" 
set out "../CAdensPCPSmixtureLOWconsformatted.pdf"


nPanels=5
set multiplot layout nPanels, 1
top = 0.993
heightY = 1.065/nPanels
bottom = top-heightY
right = 0.985
left = 0.15

zMax=4.0
set xtics format '' 1
set mxtics 4

dMax=1.2
set ytics format "%2.1f" 0.2
#set mytics 2

cMax=1000.0 #-- highest salt concentration

wCa = 2        #-- line width Ca
wCa2 = 0.5*wCa   #-- line width Ca'
dash = 2       #-- dash type Ca' 

# Colour palette to indicate ion concentrations:
set palette model RGB maxcolors 11
set palette defined (0 "#FFD300", 1 "#D70039", 2 "#0088BF", 3 "#00A368")
set key at graph 0.02, graph 0.675 left reverse Left noautotitle textcolor variable samplen -1
unset colorbox

# Indicator for Phosphorus peak positions:
pCol="#8FFF00" #-- color of phosphorus indicator lines
plw=2 #-- line width of phosphorus indicator lines

set label 1 "CHARMM36" at graph 0.05, graph 0.8 font "Arial,20" 

set rmargin at screen right
set lmargin at screen left
set tmargin at screen top
set bmargin at screen bottom
plot [0:zMax][0:dMax] \
  "../../../MATCH/Data/Lipid_Bilayers/POPS+83%popc+150mMCaCl/T298K/MODEL_CHARMM36/IONdens.xvg" \
    u (abs($1)):($2) w l lc palette frac 260/cMax lw wCa t "260 mM"



# do not write the highest tick mark on y-axis:
set ytics 0,0.2,dMax-0.2


# set label for y-axis:
set ylabel "number density of calcium ions (nm^{-3})" font "Arial,20" offset graph -0.017, graph -0.5


set label 1 "Berger"
top = bottom
bottom = bottom - heightY
set tmargin at screen top
set bmargin at screen bottom 
plot [0:zMax][0:dMax] \
  "../../../MATCH/Data/Lipid_Bilayers/POPS+80%popc+102mMCaCl/T310K/MODEL_Berger/CAdens.xvg" \
    u (abs($1)):($2) w l lc palette frac 102/cMax lw wCa t "102 mM"

unset ylabel

set label 1 "MacRog"
top = bottom
bottom = bottom - heightY
set tmargin at screen top
set bmargin at screen bottom
plot [0:zMax][0:dMax] \
  "../../../MATCH/Data/Lipid_Bilayers/POPS+83%popc+100mMCaCl/T298K/MODEL_MACROG/IONdens.xvg" \
    u (abs($1)):($2) w l lc palette frac 100/cMax lw wCa t "100 mM"


set label 1 "LIPID17"
set label 2 "ff99 ions"  at graph  0.8,0.85 tc palette frac 500/cMax
set label 3 "Dang ions"  at graph  0.8,0.7 tc palette frac 410/cMax

set xtics format '%1.0f'
set xlabel "distance to bilayer center (nm)" font "Arial,20"

top = bottom
bottom = bottom - heightY
set tmargin at screen top
set bmargin at screen bottom
plot [0:zMax][0:dMax] \
  "../../../MATCH/Data/Lipid_Bilayers/POPS+83%popc+500mMCaCl/T298K/MODEL_LIPID17/number_density_ions.dat" \
  u (abs($1)):($2) w l lc palette frac 500/cMax lw wCa t "500 mM", \
  "../../../MATCH/Data/Lipid_Bilayers/POPS+83%popc+410mMCaCl/T298K/MODEL_LIPID17/IONdens.xvg" \
  u (abs($1)):($2) w l lc palette frac 410/cMax lw wCa2 dt dash t "410 mM"


 
 

unset label 2
unset obj 2
unset multiplot