set term pdfcairo enhanced rounded color dashed dashlength 1 size 10cm, 21cm #16.2cm
set out "../HGorderparametersPS.pdf"

# write name of the blog on the plot:
# set label 1 right at screen 0.982,0.995 "nmrlipids.blogspot.fi" font ",9" tc rgb "gray"
# set tmargin at screen 0.987

set tmargin at screen 0.995
set ylabel "S_{CH}" font "Arial,20" offset 2,-2.3

set bmargin at screen 0.18 #0.31
set key l at screen 0.01,0.175 Left reverse noautotitle samplen 2 width -1 maxrows 12
#set key below horizontal left Left reverse noautotitle samplen 1 maxcols 2 maxrows 6

set lmargin at screen 0.122 #0.157
set rmargin at screen 0.99

set xtics ("{/Symbol b}" 1, "{/Symbol a}" 2, "g_3" 3, "g_2" 4, "g_1" 5) offset 0,52 scale 0 font "Arial,25"
set ytics 0.1 out nomirror
set mytics 2
dx = 0.05
r = 0.8 #-- point radius
w = 3 #-- line thickness
dxl = 0.021
set bars small # no tics in the ends of error bars

# column dividers:
set arrow 1 from 1.5, graph 0 to 1.5, graph 1 nohead lw 0.5
set arrow 2 from 2.5, graph 0 to 2.5, graph 1 nohead lw 0.5
set arrow 3 from 3.5, graph 0 to 3.5, graph 1 nohead lw 0.5
set arrow 4 from 4.5, graph 0 to 4.5, graph 1 nohead lw 0.5

# Function to get horizontal lines (for experimental data):
horizLines(filename)="< awk '{ if(substr($1,1,1)!~/#/) {print $1-0.46,$2,$3;print $1+0.46,$2,$3;print n} }'".filename

# Subjective sweet spots:
set style rectangle fc rgb "#D6F0FE" fs solid noborder
set object 1 rectangle \
         from 0.0,-0.08675 to 1.5,-0.12675 
set object 2 rectangle \
         from 1.5,-0.033528 to 2.5, 0.006472
set object 3 rectangle \
         from 1.5, 0.073216 to 2.5, 0.113216
set object 4 rectangle \
         from 2.5,-0.183 to 3.5,-0.223
set object 5 rectangle \
         from 3.5,-0.16 to 4.5,-0.20
set object 6 rectangle \
         from 4.5,-0.11 to 5.5,-0.15
set object 7 rectangle \
         from 4.5,-0.02 to 5.5,0.02

# Line- and point types for different force fields:
# GROMOS-family:
Berger = 21
set linetype Berger lw w lc rgb "#D70039" # for Berger
set linetype 32 lw w lc rgb "#FA0059" # for Tjornhammar
CKP_QB = 25
set linetype 25 lw w lc rgb "#FFA0A3" # for CKP using the same charges as in the Berger simulations, with the NH3 group having the same charges as in the N(CH3)3 group of the PC lipids
set linetype 27 lw w lc rgb "#F70000" # for Chiu
set linetype 26 lw w lc rgb "#FF8300" # for Kukol
CKP_QK = 31
set linetype 31 lw w lc rgb "#E8007C" # for CKP using the charges for the NH3 group taken from a lysine side-chain (to be more GROMOS consistent).
# between GROMOS and CHARMM:
C36_UA = 231
set linetype C36_UA lw w lc rgb "#9966B6" # for CHARMM36-UA
# CHARMM-family:
CHARMM = 23
set linetype CHARMM lw w lc rgb "#0088BF" # for CHARMM36
# between CHARMM and AMBER:
Slipid = 22
set linetype Slipid lw w lc rgb "#7AEE9E" # "#7AEE9E" # for Slipids
set linetype 28 lw w lc rgb "#9FE100" # for HoNiLy
# AMBER-family:
Lipd17 = 241
set linetype 24 lw w lc rgb "#FFD300" # for GAFF
set linetype 241 lw w lc rgb "#FFD300" # for Amber Lipid17
# OPLS-family:
MacRog = 30
set linetype 29 lw w lc rgb "#FF578F" # for OPLS-AA
set linetype MacRog lw w lc rgb "#00A368" # for MacRog

# x-displacements to avoid ovelaps:
d=0.06
dxC36do = -7*d
dxC36pK = -6*d
dxC36po = -5*d
dxCKPd2 = -3.5*d
dxCKPp2 = -2.5*d
dxCUAdo = -2*d
dxCUApo = -1*d
dxCKPdo = -0.5*d
dxCKPpo =  0.5*d
dxBERdo =  2*d
dxMcRpo =  4.5*d
dxBERpo =  3*d
dxSLIdo =  dxBERdo
dxSLIpo =  dxBERpo
dxL17do =  4*d
dxL17dJ =  5*d
dxL17po =  6*d
dxL17pJ =  7*d


# Arrows pointing data outside the graph:
#set arrow 261 from 3.17, graph 0 to 3.17, graph -0.02 lt 26 lw 3 # Kukol g3
#
# Labels for reporting the values outside the graph:
#set label 262 at 3.22, graph 0.008 rotate by 30 "0.28" font ",9" tc rgb "#FF8300" # Kukol g3

# Explanations for superscripts:
#set label 2 at screen 0.54,0.015 \
#  "^{_{(1)}} DROSS, ^{_{(2)}} APM-CP, ^{_{(3)}} R-PDLF" font ",10"
#
# DROSS = Dipolar Recoupling On-axis with Scaling and Shape preservation
# R-PDLF = R-type Proton Detected Local Field

plot [0.5:5.5][0.35:-0.35] \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/Headgroup_Glycerol_Order_Parameters_Experiments.dat') \
         u ($1):(($2)*0.00784) w l ls 8 lc rgb "#5F5F5F" lw 6.5 t "DOPS experiment T=303K", \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/Headgroup_Glycerol_Order_Parameters_Experiments.dat') \
         u ($1):(($3)*0.00784) w l ls 8 lc rgb "#5F5F5F" lw 6.5, \
     horizLines(' ../../Data/POPSorderparametersNOCaEXP.dat') \
         u ($1):($2) w l ls 4 lc 16 lw 3, \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_CHARMM36/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxC36do):($2):($3):($4) w ye lt CHARMM pt 13 ps r t 'DOPS CHARMM36 T=303K',\
     10 lc rgb "white" t "  ", \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_CHARMM36UA/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxCUAdo):($2):($3):($4) w ye lt C36_UA pt 12 ps r t 'DOPS CHARMM36-UA T=303K',\
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_SLIPIDS/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxSLIdo):($2):($3):($4) w ye lt Slipid pt 12 ps r t 'DOPS SLIPIDS T=303K', \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_GROMOS-CKP2/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxCKPd2):($2):($3):($4) w ye lt CKP_QK pt 12 ps r t 'DOPS CKP T=303K', \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_GROMOS-CKP1/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxCKPdo):($2):($3):($4) w ye lt CKP_QB pt 12 ps r t 'DOPS CKPM T=303K', \
     10 lc rgb "white" t "  ", \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_Berger/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxBERdo):($2):($3):($4) w ye lt Berger pt 13 ps r t 'DOPS Berger T=303K', \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_LIPID17/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxL17do):($2):($3):($4) w ye lt Lipd17 pt 13 ps r t 'DOPS Lipid17 T=303K', \
     "../../../MATCH/Data/Lipid_Bilayers/DOPS/T303K/MODEL_LIPID17/JCions_head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxL17dJ):($2):($3):($4) w ye lt Lipd17 pt 12 ps r t 'DOPS Lipid17 JC T=303K', \
     horizLines(' ../../Data/POPSorderparametersNOCaEXP.dat') \
         u ($1):($3) w l ls 4 lc 16 lw 3 t "POPS experiment T=298K", \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_CHARMM36/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxC36po):($2):($3):($4) w ye lt CHARMM pt 7 ps r t 'POPS CHARMM36 T=298K',\
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_CHARMM36/counterionsK_head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxC36pK):($2):($3):($4) w ye lt CHARMM pt 6 ps r t 'POPS CHARMM36 K^+ T=298K',\
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_CHARMM36UA/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxCUApo):($2):($3):($4) w ye lt C36_UA pt 6 ps r t 'POPS CHARMM36-UA T=298K',\
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_SLIPIDS/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxSLIpo):($2):($3):($4) w ye lt Slipid pt 6 ps r t 'POPS SLIPIDS T=298K', \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_GROMOS-CKP2/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxCKPp2):($2):($3):($4) w ye lt CKP_QK pt 6 ps r t 'POPS CKP T=298K', \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_GROMOS-CKP1/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxCKPpo):($2):($3):($4) w ye lt CKP_QB pt 6 ps r t 'POPS CKPM T=298K', \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_MACROG/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxMcRpo):($2):($3):($4) w ye lt MacRog pt 7 ps r t 'POPS MacRog T=298K', \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_Berger/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxBERpo):($2):($3):($4) w ye lt Berger pt 7 ps r t 'POPS Berger T=298K', \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_LIPID17/head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxL17po):($2):($3):($4) w ye lt Lipd17 pt 7 ps r t 'POPS Lipid17 T=298K', \
     "../../../MATCH/Data/Lipid_Bilayers/POPS/T298K/MODEL_LIPID17/JCions_head-glyc-OPs_ave_min_max.dat" \
         u ($1+dxL17pJ):($2):($3):($4) w ye lt Lipd17 pt 6 ps r t 'POPS Lipid17 JC T=298K'
	 
#plot [0.5:5.5][0.153471:-0.3211] \
#  "./DMPC/EXP-Gross1997.dat" u ($1):($2) w l ls 8 lc rgb "#5F5F5F" lw 6.5 \
#  t "DMPC  303K  ^{_{^{13}}}C-NMR ^{_{^{(1)}}}", \
#  "./DMPC/EXP-Dvinskikh2005b.dat" u ($1):($2) w l ls 8 lc 16 lw 3 \
#  t "  &{M}@{\"}&{DPC}314K  ^{_{^{13}}}C-NMR ^{_{^{(2)}}}", \
#  "./sortedForPlot/DMPC/BERGER-323K_blogged-24-10-13.dat" u ($1):($2) w p lt 21 pt 9 ps 1.1*r \
#  t "  &{M}@{\"}&{DPC}323K  Berger", \
#  "./sortedForPlot/DMPC/HONILY-303K_blogged-15-10-13.dat" u ($1):($2) w p lt 28 pt 8 ps 1.1*r \
#  t "  &{M}@{\"}&{DPC}303K  Högberg08", \
#  "./sortedForPlot/DLPC/CHARMM36UA-323K_blogged-24-10-14.dat" u ($1):($2)  w p lt 231 pt 10 ps 1.1*r  \
#  t "@{DLPC}{&{DMPC}}  323K  CHARMM36-UA", \
#  "./DPPC/EXP-Gally1975.dat" u ($1):($2) w l ls 16 lc rgb "#5F5F5F" lw 6.5 \
#  t "D@{PPC}{&{MPC}}  322K  ^{_{^2}}H-NMR", \
#  "./DPPC/EXP-Akutsu1981.dat" u ($1):($2) w l ls 16 lw 3 \
#  t "  &{M}@{\"}&{DPC}332K       \"", \
#  "./sortedForPlot/DPPC/BERGER-323K_blogged-13-01-15.dat"  u ($1):($2) w p lt 21 pt 5 ps r \
#  t "  &{M}@{\"}&{DPC}323K  Berger", \
#  "./sortedForPlot/DPPC/TJORNHAMMAR-323K_blogged-14-11-14.dat" u ($1):($2) w p lt 32 pt 4 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{323K}  Tjörnhammar14", \
#  "./sortedForPlot/DPPC/POGER-323K_joint.dat"   u ($1):($2):($3):($4) w ye lt 25 pt 4 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{323K}  Poger", \
#  "./sortedForPlot/DPPC/CHARMM36UA-323K_blogged-25-03-15.dat" u ($1):($2) w p lt 231 pt 4 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{323K}  CHARMM36-UA", \
#  "./sortedForPlot/DPPC/CHARMM36-323K_joint.dat"     u ($1):((130*$2+25*$3)/155):($2):($3) w ye lt 23 pt 5 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{323K}  CHARMM36", \
#  "./sortedForPlot/DPPC/SLIPIDS-323K_blogged-11-12-14.dat" u ($1):($2) w p lt 22 pt 4 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{323K}  Slipid", \
#  "./sortedForPlot/DPPC/GAFF-323K_joint.dat"        u ($1):($2):($3):($4) w ye lt 24 pt 5 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{323K}  GAFFlipid", \
#  "./POPC/EXP-Bechinger1991.dat" u ($1+dxl):($2) w l ls 4 lc rgb "#5F5F5F" lw 6.5 \
#  t "@{POPC}{&{DMPC}}  296K   ^{_{^2}}H-NMR", \
#  "./POPC/EXP-Ferreira2013.dat" u ($1+dxl):($2) w l ls 4 lc 16 lw 3 \
#  t "@{POPC}{&{DMPC}}  300K  ^{_{^{13}}}C-NMR ^{_{^{(3)}}}", \
#  "./sortedForPlot/POPC/BERGER-298K_recalculated-15-10-15.dat" u ($1):($2) w p lt 21 pt 7 ps r \
#  t "  &{M}@{\"}&{DPC}{298K}  Berger", \
#  "./sortedForPlot/POPC/CHIU-298K_recalculated-04-05-15.dat" u ($1):($2) w p lt 27 pt 6 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{298K}  Chiu", \
#  "./sortedForPlot/POPC/KUKOL-298K_blogged-20-09-14.dat" u ($1):($2) w p lt 26 pt 6 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{298K}  Kukol", \
#  "./sortedForPlot/POPC/ULMSCHNEIDER-310K_blogged-26-03-14.dat" u ($1):($2) w p lt 31 pt 6 ps r \
#  t "  &{M}@{\"}&{DPC}{310K}  Ulmschneiders", \
#  "./sortedForPlot/POPC/CHARMM36-303K_joint.dat" u ($1):($2):($3):($4) w ye lt 23 pt 7 ps r \
#  t "  &{M}@{\"}&{DPC}{303K}  CHARMM36",\
#  "./sortedForPlot/POPC/SLIPIDS-310K_blogged-12-01-15.dat" u ($1):($2) w p lt 22 pt 6 ps r \
#  t "  &{M}@{\"}&{DPC}{310K}  Slipid",\
#  "./sortedForPlot/POPC/HONILY-303K_blogged-15-10-13.dat" u ($1):($2) w p lt 28 pt 6 ps r \
#  t "  &{M}@{\"}&{DPC}{303K}  Högberg08", \
#  "./sortedForPlot/POPC/GAFF-303K_blogged-12-11-14.dat"        u ($1):($2) w p lt 24 pt 7 ps r \
#  t "  &{M}@{\"}&{DPC}@{\\ \\  \"}&{303K}  GAFFlipid", \
#  "./sortedForPlot/POPC/AMBERLIPID14-303K_blogged-26-03-14.dat" u ($1):($2) w p lt 241 pt 6 ps r \
#  t "  &{M}@{\"}&{DPC}{298K}  Lipid14",\
#  "./sortedForPlot/POPC/MACROG-310K_joint.dat" u ($1):($2):($6):($7) w ye lt 30 pt 7 ps r \
#  t "  &{M}@{\"}&{DPC}{310K}  MacRog" , \
#  x w p ls 0 ps 0 lc rgb "white" t " "

